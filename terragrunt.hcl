locals {
  # Name of the repository used for tfstate path
  repository_name = "scaffolding-terragrunt"

  # Remote state S3 Bucket ID generated by bootstrap
  bucket_id = "scaffolding-terragrunt-remote-state"

  # Remote state DynamoDB Table generated by bootstrap
  dynamodb_table_id = "scaffolding-terragrunt-remote-state"

  # ARN of assume role with access to remote state bucket and dynamodb table generated by bootstrap
  assume_role_arn = "arn:aws:iam::863139717802:role/ScaffoldingTerragrunt/ScaffoldingTerragruntRemoteStateReadWrite"

  # Automatically load account-level variables
  account_id = read_terragrunt_config(find_in_parent_folders("account.hcl"))

  # Automatically load region-level variables
  region = read_terragrunt_config(find_in_parent_folders("region.hcl"))

  # Extract the variables we need for easy access
  aws_account_id = local.account_id.locals.account_id
  aws_region     = local.region.locals.region
}

# Generate an AWS provider block
generate "provider" {
  path      = "terragrunt_generated_provider.tf"
  if_exists = "overwrite_terragrunt"

  contents  = <<EOF
provider "aws" {
  region = "${local.aws_region}"

  # Only these AWS Account IDs may be operated on by this template
  allowed_account_ids = ["${local.aws_account_id}"]
}
EOF
}

# Configure Terragrunt to automatically store tfstate files in an S3 bucket
remote_state {
  backend = "s3"

  config = {
    encrypt        = true
    bucket         = local.bucket_id
    key            = "${path_relative_to_include()}/terraform.tfstate"
    region         = local.aws_region
    dynamodb_table = local.dynamodb_table_id
    role_arn       = local.assume_role_arn
  }

  generate = {
    path      = "terragrunt_generated_backend.tf"
    if_exists = "overwrite_terragrunt"
  }
}

# Tags added to un-tagged resources.
inputs = {
  tags = {
    # { terragrunt-config = repository-name/path/to/terragrunt-file }
    terragrunt-config = "${local.repository_name}/${path_relative_to_include()}"
  }
}
